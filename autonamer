#!/usr/bin/perl
# POD {{{
=head1 NAME

autonamer - AutoNamer, Simple app that tries to correctly name media files.

=head1 SYNOPSIS

	an [options] [files...]

=head1 OPTIONS

=over 8

=item B<--case>

=item B<--no-case>

Try to clean up the filename by checking for common casing faults such as CaMeLcAsInG or FULL CAPS.
Can be negated with --no-case.

=item B<--dir [path]>

=item B<-d [path]>

By default AN will try to rename the file in place, not altering the original files path. If you wish to output the media file to an alternate directory you can specify the output path here.

=item B<--dryrun>

=item B<-n>

Do everything BUT change the filename. This is used mainly to see what AN I<would> do without actually doing it.

=item B<--fakes>

=item B<--no-fakes>

Allow fake (i.e. non-existant) files to be passed on to AutoNamer.
If this is set a list of fake files can be passed and names can be suggested. Trying to rename these files will obviously fail.
This functionality is added primerily for testing purposes.

=item B<--garbage>

=item B<--no-garbage>

Try to clean up the filename by removing all non english standard characters from the file name.
Can be negated with --no-year.

=item B<--imdb>

=item B<--no-imdb>

Try to contact IMDB to finalize details we've guessed in the earlier stages.
Can be negated with --no-imdb.

=item B<--regexp>

=item B<--no-regexp>

Apply the known regular expression hacks to try to tidy up the title.
Can be negated with --no-regexp.

=item B<--skipaf>

Skip the IMDB stage if we dont manage to clean up the file name correctly.
If not set (the default) and the autocorrection stage fails, the original filename will be passed onto IMDB in a last ditch attempt to get something meaningful.

=item B<--ui [ui]>

Sets which output interface to use. The default is 'auto'.

Possible choices are:

=over

=item auto

The default, this interface simply renames the files using the default suggestions.

=item cli

Interactive console mode. The user will be prompted upon each suggestion.

=item gtk

Use the GTK libraries to draw a graphical GUI interface.

=back

=item B<-v>

=item B<--verbose>

Be more verbose when outputting information to STDERR.
Specify multiple times to increase verbosity.

=item B<-t [string]>

=item B<--template [string]>

Overide the default output filename template.
By default the template is '%t.%e'.

The following strings will be expanded when determining the output filename:

=over

=item B<%e>

The original file extension.

=item B<%t>

The title of the media, determined after the auto correction and (optional) IMDB stages.

=item B<%y>

The year the media was made.

=back

=item B<--year>

=item B<--no-year>

Extract the year of the media from the title and use it in the subsequent IMDB query (unless --no-imdb is set)
Can be negated with --no-year.

=back

=head1 DESCRIPTION

AN is a simple file renamer that trys to apply a variety of methods to determine the true name of a given set of files.
It can optionally rename the source files (and even move them) to the output filenames.

=head1 EXAMPLES

=over

=item B<an movies/*>

Simplist use of AN.
This will scan the contents of the 'movies' directory for files matching recognized extensions. If it matches it will inteligently rename them.

=item B<an -n movies/*>

Same as above but doesn't actually do anything. This is useful to see the effect of the renaming process without actually applying the effects.

=item B<an movies/* -d .>

Scan the 'movies' directory renaming files as we go. Renamed files will be moved into the current directory.

=item B<an movies/* -d -t '%t (%y).%e>

Same as above but also renames the files using the format 'Title (year).extension' e.g. 'Troy (2004).avi'.

=back

=head1 INSTALLATION

AN requires a certain set of CPAN modules. These can be installed using the following command as root:

	cpan IMDB::Film

The module IMDB::Film has a nasty habit of failing tests. If prompted to perform these during the CPAN installation its probably best to select the default 'no' option when asked if you wish to test the module.

=head1 BUGS

Quite probably.

Please report to the author when found.

=head1 AUTHOR

Matt Carter <m@ttcarter.com>

=cut
# }}} POD

# Header {{{
package AutoNamer;
our $VERSION = '0.1.0';

use feature 'switch';
use File::Basename;
use Getopt::Long;
use IMDB::Film;
use MC::Input::Cli;
use MC::Input::Multiplexor;
use IO::Handle;
Getopt::Long::Configure('bundling', 'ignorecase_always', 'pass_through');
STDERR->autoflush(1); # } Flush the output DIRECTLY to the output buffer without caching
STDOUT->autoflush(1); # }

use Data::Dump; # FIXME: Debug use only
# }}} Header

# Options {{{
# Below is the computed best matching filters used during the RegExp cleanup process
# They are by no means perfect but seem to capture about 90% of crud passed though them
# Order is important so think about any filters you wish to add or remove since the ones below it will likely be effected

our $yearmatch = qr/.*([0-9]{4}).*/; # Year grabbing RegExp. I cannot ever see this changing but it looks nice in the options section dont you think?
our @regexps = ( # Regular expressions used to tidy up file name
	qr/www\..+?\.com/i, # Clean up www.address.com
	qr/dvdrip/i, # Simple strings we dont care about
	qr/english|italian|spanish|german|dutch/i, # Languages
	qr/\beng|fr|ita\b/i, # Short language forms
	qr/720p|AC3/, # Resolutions
	qr/final|screener|cam/i, # Types
	qr/\(.+?\)/, # Strip brackets
	qr/\[.+?\]/, # ... All kinds of brackets
	qr/\{.+?\}/, # ... Thats a lotta brackets
	qr/\<.+?\>/, # ... Still more brackets

	qr/^swghd-/, # Annoying studios that put their name first, screwing up the next RegExp
	qr/\b[a-z]+crew\b/i, # Catch common 'FooBarCrew' type names

	qr/-.*$/, # Clean up suffixes (e.g. '-Axxo')

	qr/[0-9]{4}/, # Strip remaining year stuff out of the title
);
# }}} Options

# CLI {{{
my $use_imdb = 1; # Turn on all these things by default. If the user dones't want he can use --no-imdb or --noimdb
my $use_regexp = 1;
my $use_year = 1;
my $use_garbage = 1;
my $use_case = 1;
my $template = "%t.%e";
my $ui = ($ENV{DISPLAY} ? 'gtk' : 'auto'); # Default to graphical if we have a DISPLAY setup, otherwise dumb console
my $interface = MC::Input::Multiplexor->new('UI', 'AutoNamer');
GetOptions( # Getopts, "Making Perl more confusing since 1985"
	# Global options
	'imdb!' => \$use_imdb,
	'regexp!' => \$use_regexp,
	'year!' => \$use_year,
	'garbage!' => \$use_garbage,
	'case!' => \$use_case,
	'verbose|v+' => \$verbose,
	'skip!' => \$skip_af,
	'template|t=s' => \$template,
	'dir|d=s' => \$udir, # Override dir to output to (e.g. '.')
	'dryrun|n' => \$dryrun, # Dont actually do anything
	'ui=s' => \$ui,
	'fakes!' =>\$fakes,
	'version' => \$version,
);
fatal("Invalid UI mode. --ui can be " . join(',', @{$interface->choices})) unless $ui =~ /auto|cli|gtk|debug|test/;
if ($version) {
	say(0, "AutoNamer $VERSION");
	say(0, "Written by Matt Carter <m@ttcarter.com>");
	say(0, "GIT Repo: https://github.com/hash-bang/AutoNamer");
	exit;
}

$udir =~ s/\/$//; # Strip final '/' from the output dir if the user specified one
# }}} CLI

$interface = $interface->load($ui); # Load in the output interface

@ARGV = <> unless (@ARGV); # Read files from STDIN if we didn't get anything on ARGV

FILE: while ($raw = shift) { # For each file...
	chomp $raw;
	my $dir = dirname($raw); # Extract the dir...
	$_ = basename($raw); # ...And throw it away, leaving the pure file name
	my $original = $_; # Remember the original filename before we started mangling
	my $year;

	if (! -e $raw and !$fakes) { # File doesn't exist and we care about that
		say(0, "None existant file: $raw. Use --fakes if you want to force this");
		$interface->skip('NOT_EXIST', $original, $dryrun);
		next;
	}
	unless (s/\.((?:avi|mp[0-9]|mkv|mov|qt|vob|yav|raw))$//) { # We only care about these
		say(1, "Unrecognized file type: $original. Ignoring");
		$interface->skip('UNKNOWN_EXT', $original, $dryrun);
		next FILE;
	}
	my $ext = $1; # Nasty hack to work around extracting from a s//

	say(1, "File: $original");
	(($year) = (m/$yearmatch/)) if $use_year; # Try to determine year
	if ($use_regexp) { # Apply the RegExp fixes?
		foreach $re (@regexps) {
			say(5, "Apply: [$re] to [$_]");
			s/$re//i;
			last unless $_;
		}
	}
	s/[^a-z0-9]+/ /gi if ($use_garbage); # Strip garbage characters
	s/^\s*//; s/\s*$//; # Trim fore & aft

	if ($use_case) { # Apply casing hacks
		if (/\b[A-Z][a-z]{2,}\b/) { # At least one validly cased word detected: e.g. 'Foobar'
			say(3, " * Applying case rules to: $_");
			s/\s*[A-Z]{2,}\s*//g; # Fix out of place excessive caps (e.g. 'Oliver Twist CRACKORZ!.avi')
			s/[A-Z][a-z]+[A-Z][a-zA-Z]*//g; # ClEaN CamelCasing. BeCauSe iTs BloOdY anNoyIng
			s/^\s*//; s/\s*$//; # Trim fore & aft
		} elsif (/\b[a-z]+\b/) { # All in lower case?
			s/([a-z])/\U\1\E/i; # UCFirst
		}
	}

	unless ($_) { # Nothing left?
		say(1, " * Auto correction failed");
		$interface->skip('FAILED', $original, $dryrun);
		next FILE if $skip_af; # Skip if skipaf is on
		$_ = $original; # Otherwise restore original name
	}
	my $title = $_;
	say(1, " * Guessed name: $_"); # Spew debugging info if the user cares
	say(1, " * Guessed year: $year") if $year;
	say(2, " * Ext: $ext");
	say(2, " * Dir: $dir");

	if ($use_imdb) { # Ask IMDB to tidy up what we have?
		my $film = new IMDB::Film( # Perl has a module for everything, yey Perl!
			crit => $title,
			year => $year,
		);
		if ($film->status()) {
			$title = $film->title();
			$year = $film->year();
			say(1, "IMDB result: $title ($year)");
		} else {
			say(1, "IMDB: Nothing found");
		}
	}
	say(1, " * Final name: $_");

	$renames++;
	$interface->rename($original, "$_.$ext", $dryrun, $dir, ($udir) ? $udir : $dir, $ext);
}
if ($renames) {
	$interface->finish($dryrun);
} else {
	say(0, "Nothing to do");
}

exit 0;
